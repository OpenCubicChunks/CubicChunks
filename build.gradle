buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven {url='https://dist.creeper.host/Sponge/maven'}

    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "io.github.opencubicchunks.gradle.mixingen"
apply plugin: "io.github.opencubicchunks.gradle.mcGitVersion"

mcGitVersion {
    snapshot = true
    forgeVersion = "${mc_version}-${forge_version}"
    setCommitVersion '570c0cbf0cdc15b8348a862a519d3399a943af9', '0.0'
}
group = "io.github.opencubicchunks" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "CubicChunks"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

// a hack to force gradle to run a full build to have the annotation proocessor working
// when even the compile task is up to date
// without this, refmap file isn't generated correctly
tasks.addRule("Pattern: buildNoIncremental<X>") { String taskName ->
    if (taskName.startsWith("buildNoIncremental")) {
        task(taskName) {
            tasks.withType(JavaCompile) {
                options.incremental = false
                outputs.upToDateWhen { false }
            }
            dependsOn(build)
        }
    }
}

minecraft {
    mappings channel: "snapshot", version: "${mapping}-1.15.1"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    def args = [
            "-XX:+UseG1GC",
            "-Xmx1G",
            "-XX:+UnlockExperimentalVMOptions",
            "-XX:G1NewSizePercent=20",
            "-XX:G1ReservePercent=20",
            "-XX:MaxGCPauseMillis=50",
            "-XX:G1HeapRegionSize=32M",
            "-Dmixin.debug.verbose=true",
            "-Dmixin.debug.export=true",
            "-Dcubicchunks.debug=true",
            "-XX:-OmitStackTraceInFastThrow",
            "-Dmixin.checks.interfaces=true"
    ]
    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            jvmArgs(args)

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                cubicchunks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            jvmArgs(args)

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                cubicchunks {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories{
    jcenter()
    mavenCentral()
}

configurations.getByName("annotationProcessor").extendsFrom(configurations.compileClasspath)

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}" //21

    implementation 'org.spongepowered:mixin:0.8'

    testCompile('junit:junit:4.11')
    testCompile('org.hamcrest:hamcrest-junit:2.0.0.0')

    // You may put jars on which you depend on in ./libs or you may define them like so..
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // Real examples
    // compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    // compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // The 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    // provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // These dependencies get remapped to your current MCP mappings
    // deobf 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title": "${modid}",
                "Specification-Vendor": "cubicchunks",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"cubicchunks",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConnector": "io.github.opencubicchunks.cubicchunks.mixin.CCMixinConnector"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}
mixinGen {
    filePattern = "cubicchunks.mixins.%s.json"
    defaultRefmap = "cubicchunks.refmap.json"
    defaultPackagePrefix = "io.github.opencubicchunks.cubicchunks.mixin"
    defaultCompatibilityLevel = "JAVA_8"
    defaultMinVersion = "0.8"

    config("core") {
        it.required = true
        it.conformVisibility = true
        it.injectorsDefaultRequire = 1
    }

    config("access") {
        it.required = true
        it.conformVisibility = true
    }

    config("asm") {
        it.required = true
        it.configurationPlugin = 'io.github.opencubicchunks.cubicchunks.mixin.ASMConfigPlugin'
    }

    config("optifine") {
        it.required = true
        it.configurationPlugin = 'io.github.opencubicchunks.cubicchunks.mixin.OptiFineMixinConfig'
        it.injectorsDefaultRequire = 1
    }
}
